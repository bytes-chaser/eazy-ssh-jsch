plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

archivesBaseName = 'eazy-ssh-jsch'
group 'io.github.bytes-chaser'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.bytes-chaser:eazy-ssh-core:1.0.0'
    implementation 'com.github.mwiede:jsch:0.2.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId = archivesBaseName
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'eazy-ssh-jsch'
                description = 'Eazy SSH implementation over JSch'
                url = 'https://github.com/bytes-chaser/eazy-ssh-jsch'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'bytes-chaser'
                        email = 'eazy.ssh@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/bytes-chaser/eazy-ssh-jsch.git'
                    developerConnection = 'scm:git:ssh://github.com:/bytes-chaser/eazy-ssh-jsch'
                    url = 'https://github.com/bytes-chaser/eazy-ssh-jsch'
                }
            }
        }
    }


    repositories {
        maven {
            name = "OSS"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials.username = ossUsername
            credentials.password = ossPassword
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}